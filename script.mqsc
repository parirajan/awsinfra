* =========================
*  SETTLE - Single node mTLS
* =========================

ALTER QMGR SSLKEYR('/var/mqm/qmgrs/SETTLE/ssl/key')
* ALTER QMGR CERTLABL('ibmwebspheremqsettle')
REFRESH SECURITY TYPE(SSL)

DEFINE LISTENER(SETTLE.LISTENER) TRPTYPE(TCP) CONTROL(QMGR) PORT(1414) REPLACE
START LISTENER(SETTLE.LISTENER)

DEFINE QLOCAL(PAYMENT.REQUEST)  DEFPSIST(YES) REPLACE
DEFINE QLOCAL(PAYMENT.RESPONSE) DEFPSIST(YES) REPLACE
DEFINE QLOCAL(PAYMENT.DLQ)      DEFPSIST(YES) REPLACE
ALTER QMGR DEADQ(PAYMENT.DLQ)

DEFINE CHANNEL(SETTLE.SVRCONN) CHLTYPE(SVRCONN) TRPTYPE(TCP) +
  SSLCAUTH(REQUIRED) +
  SSLCIPH('TLS_AES_256_GCM_SHA384') +
  MCAUSER('nobody') REPLACE

ALTER QMGR CHLAUTH(ENABLED)

SET CHLAUTH(SETTLE.SVRCONN) TYPE(SSLPEERMAP) +
  SSLPEER('CN=BARTER.PAYMENTS.ANCHORBANK.PVT*') +
  USERSRC(MAP) MCAUSER('mq_barter') ACTION(REPLACE)

SET CHLAUTH(SETTLE.SVRCONN) TYPE(SSLPEERMAP) +
  SSLPEER('CN=DISPENSE.PAYMENTS.ANCHORBANK.PVT*') +
  USERSRC(MAP) MCAUSER('mq_dispense') ACTION(REPLACE)

SET CHLAUTH(SETTLE.SVRCONN) TYPE(SSLPEERMAP) +
  SSLPEER('*') USERSRC(NOACCESS) ACTION(REPLACE)

SET AUTHREC PROFILE('SETTLE') OBJTYPE(QMGR) PRINCIPAL('mq_barter')   AUTHADD(CONNECT,INQ)
SET AUTHREC PROFILE('SETTLE') OBJTYPE(QMGR) PRINCIPAL('mq_dispense') AUTHADD(CONNECT,INQ)

SET AUTHREC PROFILE('PAYMENT.REQUEST')  OBJTYPE(QUEUE) PRINCIPAL('mq_barter')   AUTHADD(PUT,INQ)
SET AUTHREC PROFILE('PAYMENT.RESPONSE') OBJTYPE(QUEUE) PRINCIPAL('mq_barter')   AUTHADD(GET,BROWSE,INQ)

SET AUTHREC PROFILE('PAYMENT.REQUEST')  OBJTYPE(QUEUE) PRINCIPAL('mq_dispense') AUTHADD(GET,BROWSE,INQ)
SET AUTHREC PROFILE('PAYMENT.RESPONSE') OBJTYPE(QUEUE) PRINCIPAL('mq_dispense') AUTHADD(GET,BROWSE,INQ)

REFRESH SECURITY TYPE(CONNAUTH)
REFRESH SECURITY TYPE(AUTHSERV)
REFRESH SECURITY TYPE(SSL)

END
